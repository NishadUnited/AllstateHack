test {
    testLogging {
        events 'passed', 'failed'
        exceptionFormat 'full'
        showStandardStreams = false
    }

    reports {
        junitXml {
            destination = file("$testResultsDir/unit")
        }

        html {
            destination = file("$testReportDir/unit")
        }
    }
}

sourceSets {
    integrationTest {
        java {
            srcDir file('src/integrationTest/java')
        }

        resources {
            srcDir file('src/integrationTest/resources')
        }

        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath

    testLogging {
        events 'passed', 'failed'
        exceptionFormat 'full'
    }

    reports {
        junitXml {
            destination = file("$testResultsDir/integration")
        }

        html {
            destination = file("$testReportDir/integration")
        }
    }
}