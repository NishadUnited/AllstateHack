buildscript {
    repositories {
        maven {
            url artifactoryUrl
        }
    }

    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.3.0'
    }
}

import org.ajoberstar.grgit.*

ext {
    grgit = Grgit.open projectDir
}

task writeGitProperties(dependsOn: processResources) << {
    def file = new File(buildDir, "resources/main/git.properties")
    file.createNewFile()

    def properties = new Properties()
    properties.putAll(["git.branch"     : grgit.branch.current.name,
                       "git.commit.id"  : grgit.head().id,
                       "git.commit.time": grgit.head().time.toString()])
    properties.store(file.newWriter(), null)
}

classes.dependsOn writeGitProperties

task tagBuild << {
    grgit.tag.add {
        name = version
        message = "Build $version"
        annotate = true
        tagger = new Person(name: 'jenkins', email: 'jenkins@yourserver.com')
    }
    grgit.push tags: true
}
